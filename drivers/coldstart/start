#!/usr/bin/perl

# --------------------------------------------------------------------------------------------
# Driver: coldstart
# Operation: start
#
# Wallet setup
# - Start keosd on the current directory, logging the PID on keosd.pid
# - Create a default wallet, pipe the password to a file
# - Open and unlock the default wallet (use provided password)
# - Add development key to the wallet
#
# Start nodeos (all producers in one node), logging the PID to nodeos.pid
#
# Boot system contracts and init chain for use (requires driver install to compile
#   reference contracts once)
#
# Elect the 21 block producers
# --------------------------------------------------------------------------------------------

use strict;
use warnings;
use Time::HiRes qw(sleep);
use Scalar::Util qw(looks_like_number);

my $verbose = 1;

my $keosd_log = "keosd.log";

my $keosd_pid_file = "keosd.pid";

my $nodeos_pid_file = "nodeos.pid";

my $wallet_password_file = "default.wallet.password";

my $cleos = "cleos --wallet-url unix://./keosd.sock";

my $cmd;
my $o;

my $running_keosd_pid;    # defined to the PID of our keosd if we find it

my $running_nodeos_pid;

# --------------------------------------------------------------------------------------------
# We have to force the working directory to where the coldstart script is
# --------------------------------------------------------------------------------------------

use FindBin qw($RealBin);
if (chdir($RealBin)) {
    if ($verbose) { print "Forced working directory to coldstart start script location: $RealBin\n"; }
} else {
    print "ERROR: Failed to change working directory: $!\n";
    exit 1;
}

# --------------------------------------------------------------------------------------------
# Utils
# --------------------------------------------------------------------------------------------

sub write_to_file {
    my $file_path = shift;
    if (!defined $file_path) { die "ERROR: write_to_file: undefined file_path"; }
    my $string = shift;
    if (!defined $string) { die "ERROR: write_to_file: undefined string"; }
    open(my $file_handle, '>', $file_path) or die "ERROR: write_to_file: Could not open file '$file_path': $!";
    print $file_handle $string;
    close($file_handle);
}

sub read_from_file {
    my $file_path = shift;
    if (!defined $file_path) { die "ERROR: read_from_file: undefined file_path"; }
    local $/;
    open my $file_handle, '<', $file_path or die "ERROR: read_from_file: Could not open file '$file_path': $!";
    my $string = <$file_handle>;
    close $file_handle;
    return $string;
}

# Tries to find a keosd that would have been started from this driver directory (and thous would be
#   reachable by cleos by connecting cleos to the keosd.sock that is in this driver directory).
sub check_keosd_running {
    my $iters = shift; # how many 100ms interations to run
    if (!defined $iters) { die "ERROR: check_keosd_running: undefined iters"; }

    my $kpid;
    
    for (my $i = 0; $i < $iters; $i++) {
        $o = `ps -efww | grep \"keosd --wallet-dir $RealBin --data-dir $RealBin\" | grep -v grep | awk '{print \$2}'`;
        chomp($o);
        if (looks_like_number($o)) {
            $kpid = $o;
            if ($verbose) { print "Found keosd running as PID $kpid\n"; }
            last;
        }
        sleep(0.1);
    }

    # Check that keosd is running
    
    if (!defined $kpid) {
        return "Cannot find keosd process.\n";
    } else {
        if (kill 0, $kpid) {
            my $process_name = `ps -p $kpid -o comm=`;
            chomp($process_name);
            if ($process_name ne 'keosd') {
                return "Process $kpid does not seem to be keosd.\n";
            }    
        } else {
            return "Process $kpid not found.\n";
        }
    }

    return $kpid; # number return is the PID we found
}

# --------------------------------------------------------------------------------------------
# First, make sure the driver does not already have its nodeos running.
# This is a very simple driver that can only run one blockchain node at a time.
# --------------------------------------------------------------------------------------------

if (-e $nodeos_pid_file) {
    die "ERROR: nodeos seems to already be running (nodeos PID file '$nodeos_pid_file' exists). Must 'stop' before can 'start' again.\n";
}

# --------------------------------------------------------------------------------------------
# Wallet setup 
# --------------------------------------------------------------------------------------------

# Check that cleos and keosd exist in the system's PATH

my $no_client = 0;

$o = `which cleos`;
if ($? != 0) {
    print "cleos not found in the system's PATH.\n";
    $no_client = 1;
} else {
    if ($verbose) { print "cleos found: $o"; }
}

$o = `which keosd`;
if ($? != 0) {
    print "keosd not found in the system's PATH.\n";
    $no_client = 1;
} else {
    if ($verbose) { print "keosd found: $o"; }
}

if ($no_client) {
    print "ERROR: Cannot find wallet back-end programs. Leap must be installed.\n";
    exit 1;
}

# IF proper keosd not already running, fork this process to start a keosd instance that has
#   the default.wallet file in the same directory as the driver.

$o = check_keosd_running(1);

if (looks_like_number($o)) {

    $running_keosd_pid = $o;
    if ($verbose) { print "keosd already running on this driver's directory as PID $running_keosd_pid\n"; }
    # Don't write to file; should already be there (change to writing/overwriting the file if it turns out to be needed)

} else {

    my $pid = fork();
    die "Failed to fork: $!" unless defined $pid;

    if ($pid) {
        if ($verbose) {
            print "Trying to start keosd in a child process (log file: $keosd_log).\n";
            print "keosd's wallet directory will be coldstart's current directory.\n";
        }
    } else {
        # Run keosd while redirecting all of its output to keosd.log
        if (open(STDOUT, '>>', $keosd_log)) {
            open(STDERR, '>>&', \*STDOUT);
            my $keosd_cmd = "keosd --wallet-dir \"$RealBin\" --data-dir \"$RealBin\"";
            if ($verbose) { print "Running keosd with: $keosd_cmd\n"; }
            $o = `$keosd_cmd`;
            if ($? != 0) {
                my $exit_code = $? >> 8;
                my $error_message = strerror($exit_code);
                print "ERROR: Command failed with exit code $exit_code: $error_message\n";
            }
            close(STDOUT);
        } else {
            die "ERROR: Cannot try to run keosd: failed to redirect STDOUT: $!";
        }
        exit;
    }

    # Wait for keosd to start up (up to 3 seconds)
    $o = check_keosd_running(30);

    if (looks_like_number($o)) {
        $running_keosd_pid = $o;
        write_to_file($keosd_pid_file, $running_keosd_pid);
    }    
}

# Create the wallet file

my $wallet_files_exist = 0;

if (-e "$RealBin/default.wallet") {
    if ($verbose) { print "File '$RealBin/default.wallet' already exists.\n"; }
    $wallet_files_exist = 1;
}

if (-e "$RealBin/default.wallet.password") {
    if ($verbose) { print "File '$RealBin/default.wallet.password' already exists.\n"; }
    $wallet_files_exist = 1;
}

if ($wallet_files_exist) {

    if ($verbose) { print "One or more of the wallet file or wallet file password exists; Thus, will not attempt to recreate the wallet. Will assume this is OK and move on.\n"; }

} else {

    $cmd = "$cleos wallet create -f \"$RealBin/$wallet_password_file\"";
    
    if ($verbose) {
        print "Creating wallet $RealBin/default.wallet\n";
        print "Command: $cmd\n";
    }
    
    $o = `$cmd 2>&1`;
    if ($? != 0) {
        if ($o =~ /Error 3120001: Wallet already exists/) {
            if ($verbose) { print "Wallet file default.wallet already exists. We will assume this is OK and move on.\n"; }
        } else {
            print "Result: $o\n";
            die "ERROR: Can't create wallet.\n";
        }
    }
}

# Open the wallet

$cmd = "$cleos wallet open 2>&1";

if ($verbose) {
    print "Opening wallet $RealBin/default.wallet\n";
    print "Command: $cmd\n";
}

$o = `$cmd`;
if ($? != 0) {
    print "Result: $o\n";
    die "ERROR: Can't open wallet.\n";
}

# Unlock the wallet

my $wallet_password = read_from_file($wallet_password_file);

if ($wallet_password eq '') {
    die "ERROR: Read wallet password from '$wallet_password_file' but it's empty. Password lost; can't unlock wallet.\n";
}

$cmd = "$cleos wallet unlock --password=$wallet_password 2>&1";

if ($verbose) {
    print "Unlocking wallet $RealBin/default.wallet\n";
    print "Command: $cmd\n";
}

$o = `$cmd`;
if ($? != 0) {
    print "Result: $o\n";
    die "ERROR: Can't unlock wallet.\n";
}

# Store the developer key in the wallet


# --------------------------------------------------------------------------------------------
# Start nodeos
# --------------------------------------------------------------------------------------------

$cmd = "start_nodeos.sh";

print "Starting nodeos...\n";
print "Command: $cmd\n";

$o = `$cmd 2>&1`;

if ($? != 0) {
    print "Result: $o\n";
    die "ERROR: failed to start nodeos.\n";
}

print "Trying to read nodeos PID from $nodeos_pid_file ...\n";

$running_nodeos_pid = read_from_file($nodeos_pid_file);

print "nodeos PID is $running_nodeos_pid\n"; 

# --------------------------------------------------------------------------------------------
# Boot the chain
# --------------------------------------------------------------------------------------------


# --------------------------------------------------------------------------------------------
# Elect the BPs
# --------------------------------------------------------------------------------------------




# --------------------------------------------------------------------------------------------
# And we should be done
# --------------------------------------------------------------------------------------------



